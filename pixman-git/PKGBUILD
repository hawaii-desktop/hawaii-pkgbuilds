# $Id: PKGBUILD 160261 2012-05-31 17:12:14Z andyrtr $
# AUR Maintainer: Timoth√©e Ravier <tim@siosm.fr>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=pixman-git
_realname=pixman
pkgver=20121101
_realver=0.27.5
pkgrel=1
pkgdesc="The pixel-manipulation library for X and cairo"
arch=(i686 x86_64)
url="http://pixman.org/"
license=('custom')
depends=('glibc')
makedepends=('git')
options=('!libtool')
conflicts=('pixman')
provides=('pixman=${_realver}')

_gitroot='git://anongit.freedesktop.org/'
_gitname='pixman'

build() {
  if [ -d ${srcdir}/${_gitname} ]; then
    cd ${srcdir}/${_gitname} && git pull origin master
    msg "The local files are updated."
  else
    cd ${srcdir} && git clone ${_gitroot}${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${srcdir}/${_gitname}-build
  cp -r ${srcdir}/${_gitname} ${srcdir}/${_gitname}-build
  cd ${srcdir}/${_gitname}-build

  ./autogen.sh --prefix=/usr --disable-static
  make
}

check() {
  cd ${srcdir}/${_gitname}-build
  make check
}

package() {
  cd ${srcdir}/${_gitname}-build
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${_realname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${_realname}/"
}
